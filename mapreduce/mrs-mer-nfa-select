#!/usr/bin/perl

use Warehouse;
use Warehouse::Stream;

my $buf = "";
my $count_this_id = 0;
my $lastid = -1;
my $taqlheader;
my %pipes;
my %records;

open STDOUT, "|mrs-output" or die "pipe to mrs-output: $!";

my $batch = shift @ARGV;

while (<>)
{
    if (/^\#: taql/)
    {
	$taqlheader = $_ . qq{# field "places" "uint32"\n};
    }
    elsif (/^\#/)
    {
	$taqlheader .= $_ unless $_ eq qq{# field "ref" "sym"\n};
    }
    elsif (/^\S+\s(\d+)\s/)
    {
	my $id = $1 >> 1;
	if ($id != $lastid)
	{
	    flush();
	    $count_this_id = 0;
	}
	if (++$count_this_id > $ENV{KNOB_MAXOUTPUTSPERSAMPLE})
	{
	    $buf = "";
	}
	else
	{
	    $buf .= $_;
	}
	$lastid = $id;
    }
    else
    {
	die "No sample number in second field";
    }
}

flush();
finish();



sub flush
{
    if ($buf ne "")
    {
	while ($buf =~ /^"(.*?)" (.*\n)/gm)
	{
	    out ($1, $count_this_id." ".$2);
	}
	$buf = "";
    }
}

sub finish
{
    for (keys %pipes)
    {
	close $pipes{$_} or die "close pipes{$_}: $!";
	print STDERR "$_: ".$records{$_}." records\n";
    }
}

sub out
{
    my ($chr, $dat) = @_;
    if (!exists ($pipes{$chr}))
    {
	open $pipes{$chr}, "|gread |mrs-store ./$chr-$batch mer-nfa-placed.dat"
	    or die "open: $!";
	my $fh = $pipes{$chr};
	print $fh $taqlheader;
    }
    my $fh = $pipes{$chr};
    print $fh $dat;
    ++$records{$chr};
}

__END__

Sample usage:

KNOB_MAXOUTPUTSPERSAMPLE=3 mrs-mer-nfa-select

Sample input:

#: taql-0.1/text
# field "ref" "sym"
# field "sample" "uint32"
# field "flags" "uint32"
# field "pos0" "uint32"
# field "pos1" "uint32"
#.
"chr1" 10 4294967295 60014 62224
"chr1" 15 4294967295 92966 95429
"chr1" 18 4294967295 122810 124744
"chr2" 19 4294967295 122810 124745
"chr1" 20 4294967295 126957042 126958694
"chr1" 20 4294967295 128480 130804
"chr1" 20 4294967295 315523984 315526112
"chr1" 20 4294967295 321097056 321098714
"chr1" 21 4294967295 328923618 328925998

Sample output for chr1:

#: taql-0.1/text
# field "places" "uint32"
# field "sample" "uint32"
# field "flags" "uint32"
# field "pos0" "uint32"
# field "pos1" "uint32"
#.
1 10 4294967295 60014 62224
1 15 4294967295 92966 95429
2 18 4294967295 122810 124744
