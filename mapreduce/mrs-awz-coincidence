#!/usr/bin/perl
# -*- mode: perl; perl-indent-level: 2; -*-

#KNOBS:PREFIXBITS, SUFFIXBITS

use strict; 

my @numeric2_bp = ( 'a', 'c', 'g', 't'); 

my %bp_numeric2 = (
  'a' => 0, 
  'c' => 1,
  'g' => 2,
  't' => 3
    ); 

my @RevcTable;
foreach my $x (0 .. 255) {
  my $bp;
  my $x_rc; 
  for (my $i = 6 ; $i >= 0; $i-=2) { 
    $bp .= $numeric2_bp[($x>>$i)&3];
  }
  $bp =~ tr/acgt/tgca/;
  my $bp_rc = reverse $bp; 
  while ($bp_rc =~ m/(.)/g) {
    $x_rc = ($x_rc)<<2|$bp_numeric2{$1}; 
  }
  $RevcTable[$x]=$x_rc; 
}

my $big_vector = chr(0) x 2**$ENV{KNOB_SUFFIXBITS}; 
print STDERR "allocated ".length($big_vector)." bytes\n"; 

my $bp = $ENV{MR_INPUT};
my $prefixbits = $ENV{KNOB_PREFIXBITS}; 
my $suffixbits = $ENV{KNOB_SUFFIXBITS};
my $suffixmask = 2 ** $suffixbits -1;
my $count;
my $overflow; 

print STDERR "$bp $prefixbits $suffixbits $suffixmask\n"; 

while (read(STDIN, my $read_triple, 12)) {
  
  my ($mers0_32, $mers1_32, $id_32) = unpack "NNN", $read_triple;

  coincidence ($mers0_32, $mers1_32, revc($mers0_32), revc($mers1_32));

}
print $big_vector; 
print STDERR "counted $count with $overflow overflow\n"; 


sub revc {
  my ($read_numeric) = @_; 
  
  return ($RevcTable[$read_numeric & 0xff] << 24) | 
      ($RevcTable[($read_numeric >> 8) & 0xff] << 16) | 
      ($RevcTable[($read_numeric >> 16) & 0xff] << 8) | 
      ($RevcTable[($read_numeric >> 24) & 0xff]);
  
}

#do the work 
sub coincidence {
  my @reads = @_;
  
  foreach my $read (@reads) {
    if ($read>>$suffixbits ==$bp) {
      my $x = ord (substr ($big_vector, $read & $suffixmask, 1)); 
      if ($x < 255) {
	substr ($big_vector, $read & $suffixmask, 1) = chr($x+1); 
	$count++; 
      }
      else {
	$overflow++; 
      }
    } 
  }
}
