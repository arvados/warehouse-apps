#!/usr/bin/perl

use strict;

my $invert = 0;
for (@ARGV)
{
    if ($_ eq "-v") { $invert = 1; }
}
@ARGV = ();

$ENV{KNOB_SNPCALL_MIN_COVER} = 10 if !length $ENV{KNOB_SNPCALL_MIN_COVER};
$ENV{KNOB_SNPCALL_MIN_STARTS} = 2 if !length $ENV{KNOB_SNPCALL_MIN_STARTS};
die "no SNPREFERENCE specified" unless $ENV{KNOB_SNPREFERENCE};

open R, "-|", "bash -c 'set -o pipefail; whget -r \"$ENV{KNOB_SNPREFERENCE}\" - | gzip -cdf'"
    or die "whget: $!";
my @snpref;
while (<R>)
{
    chomp;
    push @snpref, [split];
    # special kludge for bed/dbsnp
    ++$snpref[-1]->[1]
	if ($snpref[-1]->[1]+1 == $snpref[-1]->[2]);
}
close R or die "SNPREFERENCE pipe failed: $!";

@snpref = sort { ( $a->[0] cmp $b->[0] ||
		   $a->[1] <=> $b->[1] ||
		   $#$a <=> $#$b ) } @snpref;
for (my $i=$#snpref-1; $i>=0; $i--)
{
    if ($snpref[$i]->[0] eq $snpref[$i+1]->[0] &&
	$snpref[$i]->[1] == $snpref[$i+1]->[1])
    {
	splice @snpref, $i, 1;
    }
}
my @snpskeptic = map { [] } @snpref;

my $want_snpskeptic = $ENV{KNOB_WANT_SNPS};
my $srmin = 0;
my $srmax = 0;

while(<>)
{
    my @in = split;
    if ($in[1] =~ /^chr/)
    {
	my ($chr, $pos) = @in[1,2];
	if (findsnpref ($chr, $pos, $pos + $in[-3] - 1) xor $invert)
	{
	    print;
	    feed_snpskeptic ($srmin, @in) if $want_snpskeptic;
	}
    }
    else
    {
	my ($chr, $pos) = @in[0,1];
	print if (findsnpref ($chr, $pos, $pos) xor $invert);
    }
}
if ($ENV{KNOB_SNPFILTER_ARGS})
{
    $|=1;
    open (STDOUT, "|-",
	  "mrs-maq",
	  "/usr/bin/maq.pl",
	  "SNPfilter",
	  split (/\s+/, $ENV{KNOB_SNPFILTER_ARGS}),
	  "-") or die "mrs-maq: $!";
}
print_snpskeptic() if $want_snpskeptic;

sub findsnpref
{
    my ($chr, $startpos, $endpos) = @_;

    # srmin left over from the previous search already points to the
    # next interesting position after the previous read's startpos.
    # Usually this is the relevant part of the search for this read,
    # too.  Let's check this common case quickly before doing the full
    # binary search.

    if (($chr cmp $snpref[$srmin]->[0]
	 || $endpos <=> $snpref[$srmin]->[1]) < 0
	# sought read ends before location [srmin]

	&& ($srmin == 0
	    ||
	    ($chr cmp $snpref[$srmin-1]->[0]
	     || $startpos <=> $snpref[$srmin-1]->[1]) > 0)
	# sought read starts after location [srmin-1]
	)
    {
	return undef;
    }

    # startpos and endpos tend to proceed toward srmin. perhaps they
    # got there:

    if ($chr eq $snpref[$srmin]->[0] &&
	$startpos <= $snpref[$srmin]->[1] &&
	$endpos >= $snpref[$srmin]->[1])
    {
	return 1;
    }

    # just past?

    if ($srmin < $#snpref &&
	($chr cmp $snpref[$srmin]->[0]
	 || $startpos <=> $snpref[$srmin]->[1]) > 0 &&
	($chr cmp $snpref[$srmin+1]->[0]
	 || $endpos <=> $snpref[$srmin+1]->[1]) < 0)
    {
	++$srmin;
	return undef;
    }

    $srmin = 0;
    $srmax = $#snpref;
    while ($srmin < $srmax)
    {
	my $srmid = int (($srmin + $srmax) / 2);
	my $cmp = $chr cmp $snpref[$srmid]->[0]
	    || $startpos <=> $snpref[$srmid]->[1];
	if ($cmp < 0)		# sought read starts before location [srmid] --
				# if it covers anything after [srmid], it must
				# also cover [srmid].
	{
	    $srmax = $srmid;
	}
	elsif ($cmp > 0)	# sought read starts after location [srmid] --
				# it can't cover [srmid] or anything before it.
	{
	    $srmin = $srmid + 1;
	}
	else
	{
	    $srmin = $srmid;
	    return 1;
	}
    }

    return ($chr eq $snpref[$srmin]->[0]
	    && $startpos <= $snpref[$srmin]->[1]
	    && $endpos >= $snpref[$srmin]->[1]);
}

sub feed_snpskeptic
{
    my ($snpindex, @alignment) = @_;
    my $read_start_site = $alignment[2];
    my $read_pos = $snpref[$snpindex]->[1] - $read_start_site;
    my $basecall = substr ($alignment[-2], $read_pos, 1);
    push @ { $snpskeptic[$snpindex] }, $basecall, $read_start_site;
}

sub print_snpskeptic
{
    for (my $sr = 0; $sr <= $#snpref; $sr++)
    {
	next if $snpref[$sr]->[3] eq "N";

	my $refbase = fasta2bin ($snpref[$sr]->[2]);
	my $maqcall = fasta2bin ($snpref[$sr]->[3]);
	my $newcall = $maqcall;

	$newcall = 0 if $ENV{KNOB_MINCONSENSUSQUALITY} > $snpref[$sr]->[4];

	my $sk = $snpskeptic[$sr] or next;
	my @candidate_startsites = ({}, {}, {}, {}); # a,c,g,t
	my $coverage = 0;
	while ($newcall && @$sk)
	{
	    ++$coverage;
	    my ($candidate, $read_start_site) = splice @$sk, 0, 2;
	    if (1 == ($candidate =~ tr/acgtACGT/01230123/))
	    {
		$candidate_startsites[$candidate]->{$read_start_site}++;
	    }
	}
	$newcall = 0 if $ENV{KNOB_SNPCALL_MIN_COVER} > $coverage;
	for my $candidate (0..3)
	{
	    if ($newcall > 0 &&
		($maqcall & (1 << $candidate)) &&
		($refbase & (1 << $candidate)) == 0 &&
		$ENV{KNOB_SNPCALL_MIN_STARTS}
		> scalar keys % { $candidate_startsites[$candidate] }
		)
	    {
		$newcall = 0;
	    }
	}
	if ($maqcall != $newcall)
	{
	    $snpref[$sr]->[3] = bin2fasta ($newcall);
	}
    }
    map { print join ("\t", @$_) . "\n" } @snpref;
}

sub fasta2bin
{
    my $x = shift;
    return "N" if $x =~ /^N/;
    $x =~ tr/a-z/A-Z/;
    $x =~ tr/XACMGRSVTWYHKDBN/0123456789abcdef/;
    $x = hex($x);
    while ($x & ~0xf)
    {
	$x = ($x & 0xf) | ($x >> 4);
    }
    return $x;
}

sub bin2fasta
{
    my $x = sprintf ("%x", shift);
    $x =~ tr/0123456789abcdef/XACMGRSVTWYHKDBN/;
    return $x;
}
