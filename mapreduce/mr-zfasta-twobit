#!/usr/bin/perl
# -*- mode: perl; perl-indent-level: 2; -*-

###MR_DESCRIPTION:visit all subdirectories and convert .fa (gunzip first for .fa.gz) into one big string    
###MR_INPUT:manifest

use strict; 
use Warehouse;
use Warehouse::Manifest;
use Warehouse::Stream;
use IO::Uncompress::Gunzip qw(gunzip $GunzipError);

my $manifestkey = $ENV{MR_INPUT0};

if ($ENV{MR_LEVEL} == 0)
{
  $ENV{MR_INPUT} = $manifestkey;
  do "mrtask-queuesubdirs"; 
}
elsif ($ENV{MR_LEVEL} == 1)
{
  my $wantsubdir = $ENV{MR_INPUT};
  my $whc = new Warehouse
      (memcached_size_threshold => $ENV{KNOB_MEMCACHED_SIZE});
  
  my $manifest = new Warehouse::Manifest (whc => $whc,
					  key => $manifestkey);
  $manifest->rewind;
  while (my $instream = $manifest->subdir_next)
  {
    if ($instream->name eq $wantsubdir)
    {
      my $outstream = new Warehouse::Stream (whc => $whc);
      $outstream->name ($instream->name);
      $outstream->clear;
      $outstream->write_start ("fasta.bin"); 

      while (my ($pos, $size, $filename) = $instream->file_next)
      {
	last if !defined $pos; 
	$instream->seek ($pos); 
	my $inblob; #bytes of (decompressed) file (if correct extension)
     	if ($filename =~ s/\.fa\.g?z$/\.fa/i) 
	{
	  my $zblob = ""; 
	  while (my $dataref = $instream->read_until ($pos + $size))
	  {
	    $zblob .= $$dataref; 
	  }
	  gunzip \$zblob => \$inblob or die $GunzipError;
	}
	elsif ($filename =~ m/\.fa$/i) 
	{
	  while (my $dataref = $instream->read_until ($pos + $size))
	  {
	    $inblob .= $$dataref; 
	  }
	}
	else 
	{
	  next; 
	}

	$inblob =~ s/^>.*\n//;
	$inblob =~ s/A/00/ig; 
	$inblob =~ s/C/10/ig; 
	$inblob =~ s/G/01/ig;
	$inblob =~ s/T/11/ig;
	$inblob =~ s/\n//g; 
	
	$inblob =~ s/([01]{2})([^01]+)/$1 x (1 + length($2))/ge;
	$inblob =~ s/^([^01]+)([01]{2})/$2 x (1 + length($1))/e;

		
	$outstream->write_data (pack "Lb*", length($inblob)/2, $inblob);
      }
      $outstream->write_finish; 
     
      my $fragkey = $whc->store_block ($outstream->as_string)
	  or die "store_block failed: ".$whc->errstr;
      print STDERR "+++mrout ".$fragkey."+++\n";
      print STDERR $whc->iostats;
      exit 0;
    }
  }
  die "Subdir not found: $wantsubdir";
}
