#!/usr/bin/perl

my %out_1;
my %out_2;

my $pair_select = 0 + $ENV{PAIR_SELECT};

while (<>)
{
    my @F = split;
    my $out;
    if ($ENV{KNOB_COLORSPACE}) {
	my ($csread, $csqual);
	for (@F[10..$#F]) {
	    if (/CS:Z:(.*)/) { $csread = $1; }
	    elsif (/CQ:Z:(.*)/) { $csqual = $1; }
	    last if defined $csread && defined $csqual;
	}
	next if !defined $csread || !defined $csqual;
	$csread =~ tr/0123/ACGT/;
	$csread =~ s/[^ACGT]/N/g;
	$csread =~ s/^.//;
	$out = "\@$F[0]\n$csread\n+\n$csqual\n";
    } else {
	if ($F[1] & 0x10) {
	    my $seq = reverse $F[9];
	    my $qual = reverse $F[10];
	    # complement bases
	    $seq =~ tr/acgt ryswkm bdhv ACGT RYSWKM BDHV/tgca yrswmk vhdb TGCA YRSWMK VHDB/;
	    $out = "\@$F[0]\n$seq\n+\n$qual\n";
	} else {
	    $out = "\@$F[0]\n$F[9]\n+\n$F[10]\n";
	}
    }
    if ($ENV{PAIR_SELECT} eq '*') {
	# don't try to match pairs, just print all reads.
	print $out;
    }
    elsif (!($F[1] & 1)) {
	# this is not part of a paired-end read. include it in group 0.
	print $out if $pair_select == 0;
    }
    else {
	# this is part of a paired-end read. make sure we output the
	# _1 and _2 reads in the same order, given the same input.
	if ($F[1] & 0x40) {	# first in pair
	    if (exists $out_2{$F[0]}) {
		print $out if $pair_select == 1;
		print $out_2{$F[0]} if $pair_select == 2;
		delete $out_2{$F[0]};
	    } else {
		$out_1{$F[0]} = $pair_select == 1 ? $out: 1234;
	    }
	} elsif ($F[1] & 0x80) { # second in pair
	    if (exists $out_1{$F[0]}) {
		print $out_1{$F[0]} if $pair_select == 1;
		print $out if $pair_select == 2;
		delete $out_1{$F[0]};
	    } else {
		$out_2{$F[0]} = $pair_select == 2 ? $out : 1234;
	    }
	} else {		# part of an unordered pair; print the
				# first one encountered as _1 and the
				# second one encountered as _2
	    if (exists $out_1{$F[0]}) {
		print $out_1{$F[0]} if $pair_select == 1;
		print $out if $pair_select == 2;
		delete $out_1{$F[0]};
	    } else {
		$out_1{$F[0]} = $pair_select == 1 ? $out : 1234;
	    }
	}
    }
}

if (%out_1 + %out_2) {
    warn sprintf ("giving up on %d _1 orphans and %d _2 orphans",
		  scalar (keys %out_1),
		  scalar (keys %out_2));
}
