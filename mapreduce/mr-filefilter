#!/usr/bin/perl
# -*- mode: perl; perl-indent-level: 2; -*-

###MR_DESCRIPTION:run a specified filter, one job step per input file
###MR_INPUT:manifest
###MR_KNOBS:COMMAND=
###MR_KNOBS:OUTPUTNAME=out.gz
###MR_KNOBS:CONCATENATE=0

use strict; 
use Warehouse;
use Warehouse::Manifest;
use Warehouse::Stream;
use Safepipe;

die "no command specified" if $ENV{KNOB_COMMAND} !~ /\S/;
die "no outputname specified" if $ENV{KNOB_OUTPUTNAME} !~ /\S/;

for (sort { $a <=> $b } map { /^KNOB_INPUT(\d+)$/ } keys %ENV)
{
  $ENV{MR_INPUT0} .= "," . $ENV{"KNOB_INPUT$_"};
}
my $inhashes = $ENV{MR_INPUT0};

if ($ENV{MR_LEVEL} == 0 && $ENV{KNOB_CONCATENATE})
{
  my $whc = new Warehouse;
  my $instream = new Warehouse::Stream (whc => $whc,
					hash => [split (",", $inhashes)]);
  my $manifest = "";
  while (my $dataref = $instream->read_until())
  {
    $manifest .= $$dataref;
  }
  level1 ($manifest, ".");
}
elsif ($ENV{MR_LEVEL} == 0)
{
  my $whc = new Warehouse;
  my $in = new Warehouse::Manifest (whc => $whc, key => $inhashes);
  $in->rewind;
  my $streamindex = -1;
  while (my $s = $in->subdir_next)
  {
    ++$streamindex;
    my $stream_string = $s->as_string;
    $s->rewind;
    my $fileindex = -1;
    while (my ($pos, $size, $filename) = $s->file_next)
    {
      last if !defined $pos;
      ++$fileindex;
      $stream_string =~ s/ \d+:.*/ $pos:$size:$filename/;
      if (8192 < length $stream_string) {
	print STDERR "+++mrjobstep 1 $streamindex $fileindex+++\n";
      } else {
	print STDERR "+++mrjobstep 1 $stream_string+++\n";
      }
    }
  }
  print STDERR "+++mrout d41d8cd98f00b204e9800998ecf8427e+++\n";
}
elsif ($ENV{MR_LEVEL} == 1)
{
  level1($ENV{MR_INPUT}, $ENV{MR_INPUT} =~ /^(\S+)/);
}

sub level1
{
  my $input = shift;
  my $outdir = shift;

  if ($input =~ /^(\d+) (\d+)$/) {
    # stream was too long to pass by environment variable, so we'll
    # have to read the input manifest and work on the Nth file in the
    # Mth stream.
    my ($want_streamindex, $want_fileindex) = ($1, $2);
    my $whc = new Warehouse;
    my $in = new Warehouse::Manifest (whc => $whc, key => $inhashes);
    $in->rewind;
    my $streamindex = -1;
  S:
    while (my $s = $in->subdir_next)
    {
      ++$streamindex;
      next if $want_streamindex != $streamindex;
      my $stream_string = $s->as_string;
      $s->rewind;
      my $fileindex = -1;
      while (my ($pos, $size, $filename) = $s->file_next)
      {
	last if !defined $pos;
	++$fileindex;
	next if $want_fileindex != $fileindex;
	$input =~ s/ \d+:.*/ $pos:$size:$filename/;
	last S;
      }
    }
  }


  my ($last_infile) = $input =~ /.* \d+:\d+:(\S+)/;
  my %special = ("%" => "%",
		 "N" => $ENV{MR_ID},
		 "F" => $last_infile,
		 );
  $ENV{KNOB_OUTPUTNAME} =~ s/%(.)/$special{$1}/ge;
  $ENV{LAST_INFILENAME} = $last_infile;

  # maybe I can write an output manifest without reading or writing any data blocks.
  my $want_gz = $ENV{KNOB_OUTPUTNAME} =~ /\.gz$/;
  if (($ENV{KNOB_COMMAND} eq "cat" || 0 == length $ENV{KNOB_COMMAND})
      &&
      (!$want_gz || $input =~ /\.g?z/))
  {
    my $shortcut_ok = 1;
    while ($shortcut_ok && $input =~ /(.+)/gm)
    {
      my $inputline = $1;
      $inputline =~ s/^\S+\s+//;
      my $streamlength = 0;
      my $endoffiles = 0;
      while ($shortcut_ok && $inputline =~ /(\S+)/g)
      {
	my $token = $1;
	if ($token =~ /^(\d+):(\d+):/)
	{
	  $endoffiles += $2;
	  $shortcut_ok &&= ($1 == $endoffiles);
	  $shortcut_ok &&= (!!$want_gz == ($token =~ /\.gz$/));
	}
	elsif ($token =~ /\+(\d+)/)
	{
	  $streamlength += $1;
	}
      }
      $shortcut_ok &&= $endoffiles == $streamlength;
    }
    if ($shortcut_ok)
    {
      my $whc = new Warehouse;
      $whc->write_start;
      $whc->write_data ($outdir);
      pos ($input) = undef;
      while ($input =~ /(.+)/gm)
      {
	my $inputline = $1;
	$inputline =~ s/^\S+\s+//;
	while ($inputline =~ /(\S+)/g)
	{
	  my $token = $1;
	  last if $token =~ /^(\d+):(\d+):/;
	  $whc->write_data (" ".$token);
	}
      }
      $whc->write_data (" ".$ENV{KNOB_OUTPUTNAME}."\n");
      my $outhash = join (",", $whc->write_finish);
      print STDERR "+++mrout $outhash+++\n";
      exit 0;
    }
  }

  my $child = open INSTREAM, "-|";
  die "fork failed: $!" if !defined $child;
  if (!$child)
  {
    close STDIN;
    my $whc = new Warehouse;
    pos ($input) = undef;
    while ($input =~ /(.+)/gm)
    {
      my $inputline = $1;
      my $s = new Warehouse::Stream (whc => $whc,
				     subdir => $inputline);
      $s->rewind;
      while (my ($pos, $size, $filename) = $s->file_next)
      {
	last if !defined $pos;
	$s->seek ($pos);
	while (my $dataref = $s->read_until ($pos + $size))
	{
	  print $$dataref;
	}
      }
    }
    close STDOUT or die "close child STDOUT: $!";
    exit 0;
  }
  Safepipe::readfrom ("main::S",
		      ["main::INSTREAM", "gzip -cdf"],
		      split (/\|/, $ENV{KNOB_COMMAND}),
		      $ENV{KNOB_OUTPUTNAME} =~ /\.gz$/ ? "gzip -cn" : (),
		      "mrs-store '$outdir' '$ENV{KNOB_OUTPUTNAME}'",
		      "mrs-output");
  close INSTREAM or die "close INSTREAM: $!";
  close "S" or die "close S: $!";
  while (0 <= (my $pid = wait)) { die "child $pid exited $?" if $?; }
}
