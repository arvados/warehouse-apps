#!/usr/bin/perl
# -*- mode: perl; perl-indent-level: 2; -*-

###MR_DESCRIPTION:merge bic-seq output chunks
###MR_INPUT:manifest
###MR_KNOBS:CNV
###MR_KNOBS:BICSEQ_LAMBDA=1

use strict; 
use Warehouse;
use Warehouse::Manifest;
use Warehouse::Stream;
use Safepipe;
use Fcntl ':flock';
use POSIX qw(strftime);


my $tmp = $ENV{MR_JOBSTEP_TMP};

my $verbose = 1;

my $cnv_manifest_key = $ENV{KNOB_CNV} or die;
my $cnv_path="$ENV{MR_JOB_TMP}/cnv";

my $whc = new Warehouse;
my $m = new Warehouse::Manifest (whc => $whc, key => $ENV{MR_INPUT0});
$m->rewind;

atomic_whget_tar("$cnv_manifest_key/cnv.tgz", $cnv_path);

if ($ENV{MR_LEVEL} == 0)
{
  my %chr;
  while (my $s = $m->subdir_next) {
    while (my ($pos, $size, $filename) = $s->file_next) {
      last if !defined $pos;
      if ($filename =~ /^(chr\w+)-/) {
	my $chr = $1;
	if (!exists $chr{$chr}) {
	  $chr{$chr} = 1;
	  print STDERR "+++mrjobstep 1 $chr+++\n";
	}
      }
    }
  }
  print STDERR "+++mrout d41d8cd98f00b204e9800998ecf8427e+++\n";
  exit (0);
}

else
{
  my $chr = $ENV{MR_INPUT};

  my $outdir ="$tmp/out";
  system "rm -rf $outdir";
  mkdir $outdir or die "mr-cnv-bicseq-merge: mkdir $outdir";

  chdir $tmp or die "mr-cnv-bicseq-merge: Could not chdir to $tmp";

  # Concatenate all of the bic files for this chr

  open BIC, "|egrep '^[0-9]' >$tmp/$chr.bic";
  $m->rewind;
  while (my $s = $m->subdir_next) {
    while (my ($pos, $size, $filename) = $s->file_next) {
      last if !defined $pos;
      next if $filename !~ /^\Q$chr\E-.*\.bic$/;

      $s->seek ($pos);
      while (my $dataref = $s->read_until ($pos+$size)) {
	print BIC $$dataref;
      }
    }
  }

  close BIC
      or $? == 256		# egrep exits 1 == no matches == fine
      or die "mr-cnv-bicseq-merge: BIC write failed: error $! exit $?";


  # run BIC-seq to merge the chunks

  my $bicseq_cmd = "$cnv_path/cnv/BIC-seq/a.out $chr.bic $ENV{KNOB_BICSEQ_LAMBDA} >out/$chr.bic";
  0 == system($bicseq_cmd) or die "mr-cnv-bicseq-merge: could not run $bicseq_cmd: $! (exit code: $?)";

  my $outfrag = `whput -r --as-stream $tmp/out`;
  die "mr-cnv-bicseq-merge: whput exited $?" if $?;
  print STDERR "+++mrout $outfrag+++\n";
  exit 0;
}


sub whget {
  my ($source, $target) = @_;

  if (0 != system "whget '$source' '$target'")
  {
    die "mr-cnv-bicseq-merge: whget exited $?";
  }
}

sub atomic_whget {
  my ($source, $target) = @_;

  if (open (L, "+>>", "$target.lock") &&
      flock (L, LOCK_EX) &&
      !-e ($target))
  {
    if (0 != system "whget '$source' '$target.tmp'")
    {
      system "rm -rf '$target.tmp'";
      close L;
      die "mr-cnv-bicseq-merge: whget exited $?";
    }
    rename "$target.tmp", $target;
    system "rm -rf '$target.tmp'";
  }
  close L;
  die "mr-cnv-bicseq-merge: Failed to whget $source -> $target" if !-e $target;
}

sub atomic_whget_tar {
  my ($source, $target) = @_;

  if (open (L, "+>>", "$target.lock") &&
      flock (L, LOCK_EX) &&
      !-e ($target))
  {
    if (0 != system "whget '$source' '$target.tgz.tmp'")
    {
      system "rm -rf '$target.tgz.tmp'";
      close L;
      die "mr-cnv-bicseq-merge: whget exited $?";
    }
    rename "$target.tgz.tmp", "$target.tgz" or die "mr-cnv-bicseq-merge: rename $target.tgz.tmp failed";
    mkdir "$target" or die "mr-cnv-bicseq-merge: mkdir $target failed";
    system("tar -C $target -xzf $target.tgz") == 0 or die "mr-cnv-bicseq-merge: tar -xz $target.tgz failed";
  }
  close L;
  die "mr-cnv-bicseq-merge: Failed to whget $source -> $target" if !-e $target;
}

sub atomic_whget_dir {
  my ($source, $target) = @_;

  if (open (L, "+>>", "$target.lock") &&
      flock (L, LOCK_EX) &&
      !-e ($target))
  {
    if (0 != system "whget -r '$source'/ '$target.tmp/'")
    {
      system "rm -rf '$target.tmp'";
      close L;
      die "mr-cnv-bicseq-merge: whget exited $?";
    }
    rename "$target.tmp", $target;
    system "rm -rf '$target.tmp'";
  }
  close L;
  die "mr-cnv-bicseq-merge: Failed to whget $source -> $target" if !-e $target;
}

sub emit_date {
  my $who = shift;
  my $now_string = strftime "%H:%M:%S", localtime;
  warn "$who @ $now_string\n";
}
