#!/usr/bin/perl
# -*- mode: perl; perl-indent-level: 2; -*-

use strict;
use Warehouse;
use Warehouse::Manifest;
use Warehouse::Stream;

my @fieldname;
my @fieldtype;
my %col;			# fieldname => col#
my $mercount;
my $have_answer_key;
my $answer_key_has_smallgaps;
my %answer_key_offset;

my @mersize;
my @gapmin;
my @gapmax;
for (split (/,/, $ENV{KNOB_MERS_AND_GAPS}))
{
  if (@mersize > @gapmin)
  {
    my @gap = split (/-/);
    push @gapmin, $gap[0];
    push @gapmax, $gap[-1];
  }
  else
  {
    push @mersize, $_;
  }
}

while (<>)
{
  chomp;
  if (/^\#/)
  {
    if (!defined ($mercount) && /^\# field "(.*?)" "(.*?)"/)
    {
      push @fieldname, $1;
      push @fieldtype, $2;
      $col{$1} = $#fieldname;
    }
    next;
  }
  if (!defined $mercount)
  {
    $mercount = grep { /^pos\d+$/ } @fieldname;
    print <<EOF;
#: taql-0.1/text
# field "sample" "uint32"
# field "side" "int8"
# field "snps" "int8"
# field "ref" "sym"
EOF
    ;
    for (0..$mercount-1)
    {
      print qq{\# field "pos$_" "uint32"\n};
    }
    if ($have_answer_key = exists $col{"apos0"} && exists $col{"aref"})
    {
      print qq{\# field "correct" "int8"\n};
    }
    print qq{\#.\n};
  }
  my (@fields) = split (/ /);
  my @pos = map { $fields[$col{"pos$_"}] } (0..$mercount-1);
  my $snps = grep { $fields[$col{"snppos$_"}] ne "-1" } (0..$mercount-1);
  my @out = ($fields[$col{"sample"}],
	     $fields[$col{"side"}],
	     $snps,
	     $fields[$col{"ref"}],
	     @pos);
  if ($have_answer_key)
  {
    my $correct = 1;
    $correct &&= $fields[$col{"aref"}] eq $fields[$col{"ref"}];
    $correct &&= $fields[$col{"aside"}] eq $fields[$col{"side"}];
    for (0..$mercount)
    {
      $correct &&= $fields[$col{"apos$_"}] == $pos[$_];
    }
    push @out, ($correct ? 1 : 0);
  }
  print "@out\n";
}
