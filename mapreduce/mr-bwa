#!/usr/bin/perl
# -*- mode: perl; perl-indent-level: 2; -*-

###MR_DESCRIPTION:run BWA against reference
###MR_INPUT:manifest
###MR_KNOBS:BWA
###MR_KNOBS:SAM
###MR_KNOBS:REFERENCE
###MR_KNOBS:MAX_MISMATCHES

use strict; 
use Warehouse;
use Warehouse::Manifest;
use Warehouse::Stream;
use Safepipe;
use Fcntl ':flock';


my $tmp = $ENV{MR_JOBSTEP_TMP};

my $src = "$ENV{MR_REVISION_INSTALLDIR}/src";
my $num_nodes = $ENV{SLURM_JOB_NUM_NODES} or die;
my $input_manifest_key = $ENV{MR_INPUT0} or die;
my $reference_manifest_key = $ENV{KNOB_REFERENCE} or die;
my $bwa_manifest_key = $ENV{KNOB_BWA} or die;
my $sam_manifest_key = $ENV{KNOB_SAM} or die;
my $max_mismatches = $ENV{KNOB_MAX_MISMATCHES};
$max_mismatches = 0.04 unless defined $max_mismatches;
my $reffile = "$tmp/ref";

my $bwa_path="$tmp/bwa";
my $sam_path="$tmp/sam";

my $whc = new Warehouse;

if ($ENV{MR_LEVEL} == 0)
{
  my $ref_basename;
  my $ref = new Warehouse::Manifest (whc => $whc,
				     key => $reference_manifest_key);
  while (my $s = $ref->subdir_next) {
    while (my ($pos, $size, $filename) = $s->file_next) {
      last if !defined $pos;
      $ref_basename = $s->name()."/".$1 if $filename =~ /(.*)\.bwt$/;
    }
  }
  $ref_basename =~ s{^\./}{};

  my $num_inputs = 0;
  my $in = new Warehouse::Manifest (whc => $whc,
				    key => $input_manifest_key);
  $num_inputs++ while (my $s = $in->subdir_next);

  my $inputs_per_node = $num_inputs / ($num_nodes + $num_inputs - 1);
  $in->rewind;
  while (my $s = $in->subdir_next)
  {
    my $subdir = $s->name;
    $subdir =~ s/^\.\///;
    my @files;
    while (my ($pos, $size, $filename) = $s->file_next)
    {
      last if !defined $pos;
      next if $filename !~ /(.*)\.(bfq|fq|fastq|fq.gz|fastq.gz)$/;
      # Assume _2 are reverse strand in pair-end-reads - next level does not need
      # them passed in.
      next if $1 =~ /_2$/;
      push @files, $filename;
      if (scalar(@files) >= $inputs_per_node) {
	my $filenames = join(',', @files);
	print STDERR "+++mrjobstep 1 $ref_basename $subdir $filenames+++\n";
	@files = ();
      }
    }
    if (@files) {
	my $filenames = join(',', @files);
	print STDERR "+++mrjobstep 1 $ref_basename $subdir $filenames+++\n";
    }
  }
  print STDERR "+++mrout d41d8cd98f00b204e9800998ecf8427e+++\n";
  exit 0;
}
if ($ENV{MR_LEVEL} == 1)
{
  my ($ref_basename, $subdir, $filenames) = split (/ /, $ENV{MR_INPUT}, 3);
  my @files = split(/,/, $filenames);

  if ($subdir eq ".") { $subdir = ""; }
  else { $subdir = "/$subdir"; }

  atomic_whget_tar("$bwa_manifest_key/bwa.tgz", $bwa_path);
  atomic_whget_tar("$sam_manifest_key/samtools.tgz", $sam_path);
  die "$bwa_path/bwa not found" unless -x "$bwa_path/bwa";
  atomic_whget_dir($reference_manifest_key, $reffile);

  foreach my $filename (@files) {
    my $bwa_sam_cmd;

    my $readfile1 = "$tmp/${filename}";
    my $saifile1 = "$tmp/${filename}.sai";
    my $readfile2;
    my $saifile2;

    my $is_colorspace = $filename =~ /\.cs(fasta)?\./i;
    my $ref_is_colorspace = $ref_basename =~ /\.cs(fasta)?\./i;
    die "reads are colorspace but reference is not" if $is_colorspace && !$ref_is_colorspace;
    die "reads are not colorspace but reference is" if !$is_colorspace && $ref_is_colorspace;
    my $aln_flags = $is_colorspace ? "-c" : "";

    if ($filename =~ /(.+)_1\.(.+)$/) {
      # assume paired reads
      warn "paired reads"; 
      my $filename2 = "${1}_2.$2";
      $readfile2 = "$tmp/${filename2}";
      $saifile2 = "$tmp/${filename2}.sai";
      atomic_whget("$input_manifest_key$subdir/${filename}", $readfile1);
      atomic_whget("$input_manifest_key$subdir/${filename2}", $readfile2);
      system("$bwa_path/bwa aln -t 4 -n $max_mismatches $aln_flags '$reffile/$ref_basename' $readfile1 > $saifile1") == 0
       	or die "bwa on $readfile1 failed";
      system("$bwa_path/bwa aln -t 4 -n $max_mismatches $aln_flags '$reffile/$ref_basename' $readfile2 > $saifile2") == 0
       	or die "bwa on $readfile2 failed";
      $bwa_sam_cmd = "$bwa_path/bwa sampe '$reffile/$ref_basename' $saifile1 $saifile2 $readfile1 $readfile2";
    }
    else {
      warn "single-end reads"; 
      atomic_whget("$input_manifest_key$subdir/${filename}", $readfile1);
      system("$bwa_path/bwa aln -t 4 -n $max_mismatches '$reffile/$ref_basename' $readfile1 > $saifile1") == 0
       	or die "bwa on $readfile1 failed";
      $bwa_sam_cmd = "$bwa_path/bwa samse '$reffile/$ref_basename' $saifile1 $readfile1";
    }

    Safepipe::readfrom(
      "main::BWA",
      $bwa_sam_cmd,
      "$sam_path/samtools view -S -b -",
      "mrs-store '.$subdir' '${filename}.bam'",
      "mrs-output",
    )
      or die "pipe failed: $!";
    close BWA or die "Pipe failed: $!";
    unlink $readfile1 if $readfile1;
    unlink $readfile2 if $readfile2;
    unlink $saifile1 if $saifile1;
    unlink $saifile2 if $saifile2;
  }
  exit 0;
}

sub atomic_whget {
  my ($source, $target) = @_;

  if (open (L, "+>>", "$target.lock") &&
      flock (L, LOCK_EX) &&
      !-e ($target))
  {
    if (0 != system "whget '$source' '$target.tmp'")
    {
      system "rm -rf '$target.tmp'";
      close L;
      die "whget exited $?";
    }
    rename "$target.tmp", $target;
    system "rm -rf '$target.tmp'";
  }
  close L;
  die "Failed to whget $source -> $target" if !-e $target;
}

sub atomic_whget_tar {
  my ($source, $target) = @_;

  system "rm -rf '$target'";
  if (open (L, "+>>", "$target.lock") &&
      flock (L, LOCK_EX) )
  {
    if (0 != system "whget '$source' '$target.tgz.tmp'")
    {
      system "rm -rf '$target.tgz.tmp'";
      close L;
      die "whget exited $?";
    }
    rename "$target.tgz.tmp", "$target.tgz" or die "rename $target.tgz.tmp failed";
    mkdir "$target" or die "mkdir $target failed";
    system("tar -C $target -xzf $target.tgz") == 0 or die "tar -xz $target.tgz failed";
  }
  close L;
  die "Failed to whget $source -> $target" if !-e $target;
}

sub atomic_whget_dir {
  my ($source, $target) = @_;

  system "rm -rf '$target'";
  if (open (L, "+>>", "$target.lock") &&
      flock (L, LOCK_EX))
  {
    if (0 != system "whget -r '$source'/ '$target.tmp/'")
    {
      system "rm -rf '$target.tmp'";
      close L;
      die "whget exited $?";
    }
    rename "$target.tmp", $target;
    system "rm -rf '$target.tmp'";
  }
  close L;
  die "Failed to whget $source -> $target" if !-e $target;
}
