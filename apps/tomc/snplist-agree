#!/usr/bin/perl

use strict;

my @x;
while (@ARGV && $ARGV[0] ne '--') { push @x, shift @ARGV }
shift @ARGV;
my @y = @ARGV or die "usage: $0 snplist sniplist ... -- snplist snplist ...\n";

print "<PRE>";
my @xlists;
printf "%13s", "";
for my $xfile (@x)
{
    printf "%13s", unique_part ($xfile, @x);
    push @xlists, readsnplist ($xfile);
}
my @xhash = map { readlink($_) =~ /([0-9a-f]{32})/ } @x;
my @yhash = map { readlink($_) =~ /([0-9a-f]{32})/ } @y;
my @links;
print "\n";
for my $yfile (@y)
{
    printf "%13s", unique_part ($yfile, @y);
    my $ylist_orig = readsnplist ($yfile);
    my $yhash = shift @yhash;
    my @xscore;
    my $xhighscore;
    for (my $xi=0; $xi <= $#xlists; $xi++)
    {
	my $xlist_orig = $xlists[$xi];

	my $xlist = [ @$xlist_orig ];
	my $ylist = [ @$ylist_orig ];
	my $xypoints = scalar @$xlist + scalar @$ylist;
	my $points = 0;
	while (@$xlist && @$ylist)
	{
	    my $cmp = $xlist->[0]->[0] cmp $ylist->[0]->[0]
		|| $xlist->[0]->[1] <=> $ylist->[0]->[1];
	    if ($cmp < 0)
	    {
		shift @$xlist;
		--$xypoints;
	    }
	    elsif ($cmp > 0)
	    {
		shift @$ylist;
		--$xypoints;
	    }
	    else
	    {
		# same position
		if ($xlist->[0]->[3] eq "N"
		    ||
		    $ylist->[0]->[3] eq "N")
		{
		    $xypoints -= 2;
		}
		elsif ($xlist->[0]->[3] eq "X"
		       ||
		       $ylist->[0]->[3] eq "X")
		{
		    $xypoints -= 2;
		}
		elsif ($xlist->[0]->[3] eq
		       $ylist->[0]->[3])
		{
		    # same bp at this position
		    $points += 2;
		}
		elsif (($xlist->[0]->[3] &
			$ylist->[0]->[3]) != 0)
		{
		    # some overlap in choices
		    $points += 0;
		}
		shift @$xlist;
		shift @$ylist;
	    }
	}
	my $score = sprintf "%.2f", 100*$points/$xypoints;
	push @xscore, $score;
	$xhighscore = $score if $xhighscore < $score;
    }
    for (my $xi=0; $xi <= $#xscore; $xi++)
    {
	my $star = $xscore[$xi] > $xhighscore*.9;
	printf "%13s", ($star ? "*" : " ").$xscore[$xi];
	if ($star)
	{
	    my $xhash = $xhash[$xi];
	    my $label = &unique_part ($yfile, @y)
		. " / " . &unique_part ($x[$xi], @x);
	    push @links, qq{$label - <a href="http://genomerator-dev.freelogy.org/pgp10factory/snpdig.cgi/$yhash;agree-$xhash">agree</a> / <a href="http://genomerator-dev.freelogy.org/pgp10factory/snpdig.cgi/$yhash;disagree-$xhash">disagree</a>};
	}
    }
    print "\n";
}
print "</pre>";
print "<UL>\n<LI>".join("</LI>\n<LI>", @links)."</LI>\n</UL>";

sub readsnplist
{
    my $file = shift;
    open IN, "<", $file or die "$file: $!";
    my @snplist;
    while (<IN>)
    {
	chomp;
	my ($chr, $pos, $refbp, $seqbp, $extra) = split (/\t/, $_, 5);
	push @snplist, [$chr, $pos, $refbp, fasta2bin ($seqbp), $extra]
	    if ($pos =~ /^\d+$/ && $refbp =~ /^.$/);
    }
    return [ sort { $a->[0] cmp $b->[0] || $a->[1] <=> $b->[1] } @snplist ];
}

sub fasta2bin
{
    my $fasta = shift;
    return "N" if $fasta =~ /^N/;
    return "X" if $fasta eq "X";
    $fasta =~ tr/a-z/A-Z/;
    $fasta =~ tr/XACMGRSVTWYHKDBN/0123456789abcdef/;
    $fasta = hex($fasta);
    while ($fasta & ~0xf)
    {
	$fasta = ($fasta & 0xf) | ($fasta >> 4);
    }
    return $fasta;
}

sub bin2fasta
{
    my $x = shift;
    $x = sprintf ("%x", $x);
    $x =~ tr/0123456789abcdef/XACMGRSVTWYHKDBN/;
    return $x;
}

sub unique_part
{
    my $this = shift;
    my $thislen = length($this);
    my $ustart = $thislen;
    my $uend = $thislen;
    for (@_)
    {
	$ustart-- while substr ($this, 0, $ustart) ne substr ($_, 0, $ustart);
	$uend-- while $uend && substr ($this, $thislen-$uend) ne substr ($_, length($_)-$uend);
    }
    return substr ($this, $ustart, $thislen - $uend - $ustart);
}
