#!/usr/bin/perl

use Warehouse;
use Warehouse::Manifest;

my %knob = ( prefixbits => 3,
	     suffixbits => 21,
	     k => 8,
	     individuals => 1,
	     correctbits => "66666555",
	     reducebits => "44444333",
	     bp => 3142044949,
	     ploidy => 2,
	     snpbits => 11,
	     seed => 12345,
	     reference => "88f0fab736bdf704a559ae48ade5a423",
	     biggap => "750-1261",
	     maxreadlength => 18,
	     snps => 1,
	     all => 1,
	     inputstream => "./0",
	     batchsize => 100000,
	     maxbatches => 0,
	     skipnosnps => 0,
	     skipdegenerates => 0,
	     stepspernode => 4,
	     maxoutputspersample => 63,
	     photons => 1,
	     nodes => 12,
	     warehouse => "templeton",
	     );

for (@ARGV)
{
    if (/(.*?)=(.*)/)
    {
	warn "Warning: unknown knob \"$1\" ignored\n" if !exists $knob{$1};
	$knob{$1} = $2;
    }
    else
    {
	die "usage: $0 knob=value knob=value ...\n";
    }
}

my $svninfo = `svn info http://tomc.oxf.freelogy.org/repos/tomc/polony-tools`;
$knob{revision} = $1
    if !exists $knob{revision} && $svninfo =~ /\nRevision: (\d+)\n/;

$whc = new Warehouse (warehouse_name => $knob{warehouse});

make_place_stats() if !$knob{place_stats_output};

my $nreads = 0;
my $nreads_maxplacements = 0;
my $nreads_anysnps = 0;
my $nreads_0snps = 0;
my $nreads_0or1snps = 0;
my $nreads_incorrect_anysnps = 0;
my $nreads_incorrect_0snps = 0;
my $nreads_incorrect_0or1snps = 0;
my $nplacements_0or1snps = 0;
my $nplacements = 0;
my $unique = 0;
my $nreads_correct = 0;
my $nplacements_0snps = 0;
my $unique_and_correct = 0;
my $unique_and_correct_0snps = 0;
my $unique_and_correct_0or1snps = 0;
my @nreads_vs_log_placements;
my @nreads_0snps_vs_log_placements;
my @nreads_0or1snps_vs_log_placements;
my $manifest = new Warehouse::Manifest (whc => $whc,
					key => $knob{place_stats_output});
while (my $stream = $manifest->subdir_next)
{
    while (my ($pos, $size, $filename) = $stream->file_next)
    {
	last if !defined $pos;
	while (my $dataref = $stream->read_until ($pos + $size, "\n"))
	{
	    next if $$dataref =~ /^\#/;
	    my @s = split (/ /, $$dataref);
	    ++$nreads;
	    ++$nreads_maxplacements, next
		if $knob{maxoutputspersample} > 0
		&& $s[3] >= $knob{maxoutputspersample};
	    ++$nreads_correct if $s[1];
	    ++$unique if $s[3] == 1;
	    ++$unique_and_correct if $s[3] == 1 && $s[1];
	    ++$unique_and_correct_0snps if $s[3] == 1 && $s[1] && $s[2];
	    ++$unique_and_correct_0or1snps if $s[3] == 1 && $s[1] && $s[4];

	    if ($s[2] > 0)
	    {
		++$nreads_0snps;
		++$nreads_0snps_vs_log_placements[int(log($s[2])/log(2))];
		$nplacements_0snps += $s[2];
	    }
	    if ($s[3] > 0)
	    {
		++$nreads_anysnps;
		++$nreads_vs_log_placements[int(log($s[3])/log(2))];
		$nplacements += $s[3];
	    }
	    if ($s[4] > 0)
	    {
		++$nreads_0or1snps;
		++$nreads_0or1snps_vs_log_placements[int(log($s[4])/log(2))];
		$nplacements_0or1snps += $s[4];
	    }
	    if ($s[3] == 1)
	    {
		++$nreads_incorrect_0snps if $s[2] > $s[5];
		++$nreads_incorrect_0or1snps if $s[4] > $s[6];
		++$nreads_incorrect_anysnps if $s[1] == 0;
	    }
	}
    }
}
printf (<<EOF, $nreads, $nreads_maxplacements, $knob{maxoutputspersample}, $nplacements_0snps, $nplacements_0or1snps, $nplacements, $nreads_0snps, $nreads_0or1snps, $nreads_anysnps, $nreads_correct, $unique, $unique_and_correct_0snps, $unique_and_correct_0or1snps, $unique_and_correct, $nreads_incorrect_0snps, $nreads_incorrect_0or1snps, $nreads_incorrect_anysnps);
%12d reads placed
%12d reads omitted from remainder of stats because they placed >= %d times
%12d placements with 0 SNPs
%12d placements with <2 SNPs
%12d placements with <2 SNPs per arm
%12d reads placed with 0 SNPs
%12d reads placed with <2 SNPs
%12d reads placed with <2 SNPs per arm
%12d reads placed in the location specified by the answer key
%12d reads placed only once
%12d reads placed only once, == answer key, 0 SNPs
%12d reads placed only once, == answer key, <2 SNPs
%12d reads placed only once, == answer key, <2 SNPs per arm
%12d reads placed only once, != answer key, 0 SNPs
%12d reads placed only once, != answer key, <2 SNPs
%12d reads placed only once, != answer key, <2 SNPs per arm
EOF
printf ("%4s %12s %12s %12s %s\n",
	"P", "#reads", "#<2snps", "#exact",
	"# = #placed 2^P .. 2^(P+1)-1 times");
for (0..$#nreads_vs_log_placements)
{
    printf ("%4d %12d %12d %12d\n",
	    $_,
	    $nreads_vs_log_placements[$_],
	    $nreads_0or1snps_vs_log_placements[$_],
	    $nreads_0snps_vs_log_placements[$_],
	    );
}

sub make_fasta_personalize
{
    my $jobid;

    my $nodes =
	int (($knob{individuals} * $knob{ploidy} + $knob{stepspernode} - 1)
	     / $knob{stepspernode});
    $nodes = $knob{nodes} if $nodes > $knob{nodes};

    $jobid = $whc->job_new (mrfunction => "fasta-personalize",
			    revision => $knob{revision},
			    inputkey => $knob{reference},
			    knobs => join ("\n",
					   map { uc($_) . "=" . $knob{$_} }
					   qw(individuals
					      ploidy
					      snpbits
					      seed
					      stepspernode)),
			    nodes => $nodes,
			    photons => $knob{photons},
			    )
	or die $whc->errstr;
    warn "submitted fasta-personalize r$knob{revision} job $jobid\n";
    $knob{fasta_personalize_output} = wait_for_output ($jobid);
}

sub make_fasta_reads
{
    make_fasta_personalize() if !$knob{fasta_personalize_output};

    my $jobid;
    $jobid = $whc->job_new (mrfunction => "zfasta-reads",
			    revision => $knob{revision},
			    inputkey => $knob{fasta_personalize_output},
			    knobs => join ("\n",
					   map { uc($_) . "=" . $knob{$_} }
					   qw(prefixbits
					      suffixbits
					      k
					      individuals
					      bp
					      correctbits
					      reducebits
					      ploidy
					      seed
					      stepspernode)),
			    nodes => ($knob{nodes} > 4 ? 4 : $knob{nodes}),
			    photons => $knob{photons},
			    )
	or die $whc->errstr;
    warn "submitted zfasta-reads r$knob{revision} job $jobid\n";
    $knob{fasta_reads_output} = wait_for_output ($jobid);
}

sub make_place
{
    make_fasta_reads() if (!$knob{fasta_reads_output});

    my $bigmer = 2 * $knob{k};
    $knob{mers_and_gaps} = "$bigmer,$knob{biggap},$bigmer";
    $knob{smallgapmin} = $knob{maxreadlength} - 2*$knob{k} - 1;
    $knob{smallgapmax} = $knob{maxreadlength} - 2*$knob{k};
    $knob{smallgappos} = $knob{k};
    $knob{checkanswerkey} = 1;
    my $jobid;
    $jobid = $whc->job_new (mrfunction => "place",
			    revision => $knob{revision},
			    inputkey => $knob{fasta_reads_output},
			    knobs => join ("\n",
					   map { uc($_) . "=" . $knob{$_} }
					   qw(reference
					      checkanswerkey
					      mers_and_gaps
					      smallgapmin
					      smallgapmax
					      smallgappos
					      inputstream
					      batchsize
					      maxbatches
					      skipnosnps
					      skipdegenerates
					      maxoutputspersample
					      stepspernode
					      snps
					      all)),
			    nodes => $knob{nodes},
			    photons => $knob{photons},
			    )
	or die $whc->errstr;
    warn "submitted place r$knob{revision} job $jobid\n";
    $knob{place_output} = wait_for_output ($jobid);
}


sub make_place_stats
{
    make_place() if (!$knob{place_output});

    my $jobid;
    $jobid = $whc->job_new (mrfunction => "place-stats",
			    revision => $knob{revision},
			    inputkey => $knob{place_output},
			    knobs => "",
			    nodes => 1,
			    photons => $knob{photons},
			    )
	or die $whc->errstr;
    warn "submitted place-stats r$knob{revision} job $jobid\n";
    $knob{place_stats_output} = wait_for_output ($jobid);
}

sub wait_for_output
{
    local $_ = select STDERR; $|=1; select $_;
    my $jobid = shift;
    die "no job id" if !defined $jobid;
    my $started = 0;
    my $status_last_iter = -1;
    while (1)
    {
	sleep 10;
	my $joblist = $whc->job_list (id_min => $jobid, id_max => $jobid);
	my $job = $joblist->[0];
	if ($job->{outputkey} && $job->{success})
	{
	    print STDERR "\njobid $jobid output ".$job->{outputkey}."\n";
	    return $job->{outputkey};
	}
	elsif ($job->{finishtime})
	{
	    die "\njob $jobid failed or had no output: ".$job->{metakey}."\n";
	}
	elsif ($job->{starttime})
	{
	    if (!$started)
	    {
		print STDERR "\nStarted ".localtime()."\n";
		$started = 1;
	    }
	    if (my $steps_total
		= $job->{steps_todo}
		+ $job->{steps_running}
		+ $job->{steps_done})
	    {
		my $status_serial = join (",",
					  $job->{steps_todo},
					  $job->{steps_running},
					  $job->{steps_done});
		next if $status_last_iter eq $status_serial;
		$status_last_iter = $status_serial;

		my $w = 60;
		my $bar = "." x $w;
		my $done = int ($job->{steps_done} * $w / $steps_total);
		my $run = int ($job->{steps_running} * $w / $steps_total);
		substr $bar, 0, $done, "=" x $done;
		substr $bar, $done, $run, ">" x $run;
		$bar .= sprintf (" %-17s",
				 $job->{steps_done}.
				 "+".$job->{steps_running}.
				 "/".$steps_total);
		print STDERR "\r$bar";
	    }
	    else
	    {
		print STDERR ">";
	    }
	}
	else
	{
	    print STDERR ",";	# not running yet
	}
    }
}
