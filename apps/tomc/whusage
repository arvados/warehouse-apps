#!/usr/bin/perl

use POSIX;

my $summary_from;
my $summary_to;
my $nodeseconds;
my %interval_nodeseconds;
while(<>)
{
    my %job;
    foreach (split (/, /))
    {
	my ($k, $v) = split (/=/, $_, 2);
	$job{$k} = $v;
    }
    my $starttime = parsetime ($job{"starttime"});
    my $finishtime = parsetime ($job{"finishtime"});
    if ($starttime && $finishtime)
    {
	$summary_from = $starttime
	    if !defined $summary_from || $summary_from > $starttime;
	$summary_to = $finishtime
	    if !defined $summary_to || $summary_to < $finishtime;
	my $nnodes = countnodes ($job{"nodes"});
	my $elapsed = $finishtime - $starttime;

	next unless $nnodes > 0;

	for (my $interval = $starttime - ($starttime % 3600);
	     $interval < $finishtime;
	     $interval += 3600)
	{
	    my $segment_start = $interval;
	    my $segment_end = $interval + 3600;
	    $segment_start = $starttime if $segment_start < $starttime;
	    $segment_end = $finishtime if $segment_end > $finishtime;
	    my $seconds_this_interval = $segment_end - $segment_start;
	    $interval_nodeseconds{$interval} +=
		$seconds_this_interval * $nnodes;
	}
	$nodeseconds += $nnodes * $elapsed;
    }
}
for (sort { $a <=> $b } keys %interval_nodeseconds)
{
    print ($interval_nodeseconds{$_}, "\t", scalar localtime($_), "\n");
}
print ($summary_to - $summary_from,
       "\tinterval: ",
       scalar localtime ($summary_from),
       " to ",
       scalar localtime ($summary_to),
       "\n");
print "$nodeseconds\ttotal seconds * nodes\n";
exit;


sub parsetime
{
    my $str = shift @_;
    return undef if $str !~ /\S/;
    my @ymdhms = split (/[- :]/, $str);
    my $t = mktime (@ymdhms[5,4,3,2], $ymdhms[1]-1, $ymdhms[0]-1900);
    return $t;
}


sub countnodes
{
    my $str = shift @_;
    if ($str !~ /\D/) { return $str; }
    my $n = 0;
    while ($str =~ s/^[^\[,]+(\[([-\d,]+)\])?,?//)
    {
	if ($2)
	{
	    foreach (split (/,/, $2))
	    {
		my ($a, $b) = split /-/;
		if ($b) { $n += 1+$b-$a; }
		else { $n++; }
	    }
	}
	else { $n++; }
    }
    $n;
}
