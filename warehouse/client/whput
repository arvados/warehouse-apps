#!/usr/bin/perl

=head1 NAME

whput - store a file or directory into the warehouse

=head1 SYNOPSIS

whput [options] localdir
whput [options] localfile
whput [options] -

Options:

    --warehouse-controllers=host:port
    --mogilefs-trackers=host:port,host:port
    --name=/manifestname
    --in-manifest or --as-manifest
    --as-stream
    --use-filename=read-from-stdin.txt
    --progress
    --batch-progress
    --progress-interval=n_seconds

=head1 DESCRIPTION

B<whput> allows storage of a single data set from a file, or a collection from a directory.  In the directory case, a manifest will be created to describe the directory structure.

Normally the B<locator> of the data set or manifest is printed to standard output.  This locator can then be used with whget(1p).

=head1 OPTIONS

=over

=item --name=I<string>

Attach the resulting manifest hash to a global name.

=item --as-stream

Output a manifest to standard output (instead of storing a manifest in the warehouse, in the directory case).

=item --in-manifest

Wrap a single file in a manifest (as if it was in a directory).

=item --progress

Display progress indicator on stderr (suitable for tty).

=item --batch-progress

Display progress indicator on stderr (suitable for scripts).

=back

=cut

use strict;

use Warehouse;
use Warehouse::Stream;
use Digest::MD5;
use HTTP::Request::Common;
use LWP::UserAgent;

while ($ARGV[0] =~ /^-(?:-(\S+?)(=(.*))?|([^-]+))$/)
{
    if ($4) { map { $main::opt{$_}++ } split ("", $4); }
    else { $main::opt{$1} = defined ($2) ? $3 : 1; }
    shift @ARGV;
}
print STDERR map { "opt{$_} = $main::opt{$_}\n" } keys %main::opt
    if $main::opt{v} >= 3;
$main::opt{"progress-interval"} = 1 if $main::opt{"progress-interval"} < 1;


my $localbase = shift @ARGV or usage();
@ARGV == 0 or usage();

my $whc = new Warehouse ("warehouse_servers" => $main::opt{"warehouse-controllers"},
			 "mogilefs_trackers" => $main::opt{"mogilefs-trackers"},
			 "debug_mogilefs_paths" => ($main::opt{v} >= 3),
			 );
my $manifest = "";
$main::data_bytes_written = 0;

my $manifest_key;
if ($localbase eq "-" || -f $localbase)
{
    $manifest_key = write_file ($localbase);
}
else
{
    traverse ($localbase, "", ".", \$manifest);

    if ($main::opt{"as-stream"})
    {
	print $manifest;
    }
    else
    {
	$whc->write_start;
	$whc->write_data ($manifest) or die "Failed to write manifest: ".$whc->errstr;
	$manifest_key = $whc->write_finish or die "Failed to write manifest: ".$whc->errstr;

	display_progress(1);
	print "$manifest_key\n";
    }
}

if (my $manifestname = $main::opt{"name"})
{
    if (defined $manifest_key)
    {
	print STDERR "storing: $manifestname => $manifest_key\n" if $main::opt{"verbose"};

	my $oldkey = $whc->fetch_manifest_key_by_name ($manifestname);
	$whc->store_manifest_by_name ($manifest_key, $oldkey, $manifestname);

	my $checkkey = $whc->fetch_manifest_key_by_name ($manifestname);
	print STDERR "$manifestname => $checkkey\n" if $main::opt{"verbose"};
    }
    else
    {
	die "No manifest key to store; ignoring --name request"
    }
}


sub write_file
{
    my $filename = shift;
    if ($filename eq "-")
    {
	open F, "<&STDIN" or die "Can't dup stdin: $!";
    }
    else
    {
	open F, "<", $filename or die "Can't open $filename: $!";
    }
    my $use_filename = ($main::opt{"use-filename"} || (split("/", $filename))[-1]);
    die "Stored filename must not end with /" if $use_filename =~ m:/$:;
    $use_filename =~ s:^/+::;
    my $use_subdir = ".";
    if ($main::opt{"use-subdir"})
    {
	$use_subdir = $main::opt{"use-subdir"};
    }
    elsif ($use_filename =~ s:(.*)/(.*):$2:)
    {
	$use_subdir = $1;
    }
    $use_subdir =~ s:^/+::;
    $use_subdir = "./$use_subdir" if $use_subdir !~ m:^\.(/.*)?$:;
    while ($use_subdir =~ s:/\.\./:/:) { }
    $use_subdir =~ s://+:/:g;
    $use_subdir =~ s:/$::;

    my $s = new Warehouse::Stream (whc => $whc);
    $s->name ($use_subdir);
    $s->clear;
    $s->write_start ($use_filename);
    my $buf;
    my $ok;
    while ($ok = sysread F, $buf, 2**23)
    {
	$s->write_data (\$buf) or die "Warehouse::Stream::write_data failed: ".$s->errstr;
	$main::data_bytes_written += length $buf;
	display_progress();
    }
    die "Error reading $filename: $!" if !defined $ok;
    close F;
    $s->write_finish;
    display_progress(1);
    if ($main::opt{"in-manifest"} || $main::opt{"as-manifest"})
    {
	$whc->write_start;
	$whc->write_data ($s->as_string);
	my $k = join (",", $whc->write_finish);
	print "$k\n";
	return $k;
    }
    elsif ($main::opt{"as-stream"})
    {
	print $s->as_string;
	return undef;
    }
    else
    {
	my $k = $s->as_key;
	print "$k\n";
	return $k;
    }
}


sub traverse
{
    my ($localbase, $localsubdir, $remotedir, $manifestref) = @_;
    my $localdir = $localbase . $localsubdir;
    my @subdir;
    my $streampos = 0;
    my @file_positions;

    $whc->write_start;
    opendir D, "$localdir" or die "Can't open $localdir: $!";
    foreach my $file (sort readdir D)
    {
	if ($file eq '.' || $file eq '..')
	{
	    next;
	}
	if (-l "$localdir/$file")
	{
	    warn "Skipped symbolic link: $localdir/$file\n";
	    next;
	}
	if (-d "$localdir/$file")
	{
	    push @subdir, $file;
	    next;
	}
	printf STDERR ("storing %s\n", "$localdir/$file") if $main::opt{v} >= 3;
	my $filesize = send_file ($whc, "$localdir/$file");
	push @file_positions, "${streampos}:${filesize}:${file}";
	$streampos += $filesize;
    }
    closedir D;
    my @stream_hashes = $whc->write_finish;

    if (@file_positions) {

	die "Write failed: ".$whc->errstr if !@stream_hashes;

	for (@file_positions) { s/ /_/g; }

	my $subdir_stream = "$remotedir @stream_hashes @file_positions\n";
	$$manifestref .= $subdir_stream;

	printf STDERR ("$remotedir: stored %d files in %d blocks\n",
		       scalar @file_positions,
		       scalar @stream_hashes)
	    if $main::opt{v} >= 1;

	printf STDERR $whc->iostats 
	    if $main::opt{v} >= 2;
    }

    foreach my $subdir (@subdir)
    {
	traverse ($localbase,
		  "$localsubdir/$subdir",
		  "$remotedir/$subdir",
		  $manifestref);
    }
}

sub md5_file
{
    my $file = shift;
    open FILE, "<$file" or die "Can't open $file: $!";
    my $m = Digest::MD5->new;
    my $buf;
    my $bytes;
    while ($bytes = read FILE, $buf, 1048576) {
	$m->add($buf);
    }
    if (!defined $bytes)
    {
	die "Read error: $file: $!";
    }
    close FILE or die "Read error: $file: $!";
    return $m->digest;
}

sub binhex
{
    unpack ("H*", shift @ARGV);
}

sub send_file
{
    my $whc = shift;
    my $file = shift;
    open FILE, "<$file" or die "Can't open $file: $!";
    my $buf;
    my $bytes;
    my $totalsize = 0;
    while ($bytes = read FILE, $buf, 1048576) {
	$whc->write_data ($buf) or die "Warehouse::write_data failed";
	$totalsize += $bytes;
	$main::data_bytes_written += $bytes;
	display_progress();
    }
    if (!defined $bytes)
    {
	die "Read error: $file: $!";
    }
    close FILE or die "Read error: $file: $!";
    return $totalsize;
}

sub display_progress
{
    if (@_ || $main::last_display_progress <= int(time) - $main::opt{"progress-interval"}) {
	printf STDERR "\r%d bytes written", $main::data_bytes_written if $main::opt{"progress"};
	printf STDERR "%d bytes written\n", $main::data_bytes_written if $main::opt{"batch-progress"};
	$main::last_display_progress = int(time);
	print STDERR "\n" if @_ && $main::opt{"progress"};
    }
}

sub usage
{
    die qq{
usage:
    $0 [options] localdir
    $0 [options] localfile
    $0 [options] -
options:
    --warehouse-controllers=host:port
    --mogilefs-trackers=host:port,host:port
    --name=/manifestname
    --in-manifest or --as-manifest
    --as-stream
    --use-filename=read-from-stdin.txt
    --use-subdir=./subdir/in/manifest

};
}
