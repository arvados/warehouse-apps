#!/usr/bin/perl

use strict;
use Digest::MD5 'md5_hex';
use HTTP::Request::Common;
use LWP::UserAgent;

my %defopts = qw(
		 domain    images
		 class     single
		 positions ./position_list.dat
		 cycles    ./all_cycles.cfg
		 localdir  .
		 );
my %opts = %defopts;
while ($ARGV[0] =~ /^-/)
{
    if ($ARGV[0] =~ /^--(.*?)(=(.*))?$/)
    {
	$opts{$1} = defined($3) ? $3 : 1;
    }
    else
    {
	$ARGV[0] =~ /^-(.*)/;
	$opts{$1} = 1;
    }
    shift @ARGV;
}

if (@ARGV != 2)
{
    print STDERR <<EOF;
usage:
       $0 [options] keyprefix http://host/path/to/uploadscripts
options:
       --localdir=/path/to/images/raw          default = $defopts{localdir}
       --cycles=/path/to/all_cycles.cfg        default = $defopts{cycles}
       --positions=/path/to/position_list.dat  default = $defopts{positions}
       --domain=mogilefsdomain        default = $defopts{domain}
       --class=mogilefsclass          default = $defopts{class}
       --quick                        don't compare md5 of existing files
       -n                             don't really upload anything
       -v                             verbose
examples:
       $0 /tmp/RAW /foo_01/IMAGES/RAW http://tomc
EOF
;
    exit 1;
}

my ($keyprefix, $remote) = @ARGV;
for ($keyprefix)
{
  s|/*$||;
}

my $ua = LWP::UserAgent->new;

if ($opts{v}) { warn "Getting manifest.\n"; }

my %storedmd5;
my $r = $ua->request(POST $remote."/manifest.php",
		     [ "keyprefix" => $keyprefix."/",
		       "domain" => $opts{domain},
		       "class" => $opts{class} ]);
foreach (split (/\n/, $r->content))
{
    my ($md5, $key) = split;
    $storedmd5{$key} = $md5;
}

if ($opts{v}) { warn "Reading position list.\n"; }

open POSITIONS, "<$opts{positions}" or die "Can't open $opts{positions}: $!\n";
my @positions = <POSITIONS>;
close POSITIONS;
my $nframes = 0;
foreach (@positions)
{
    my ($frame_id, $x, $y, $w, $h) = split;
    if ($frame_id !~ /^\d+$/) { next; }
    ++$nframes;
}

if ($opts{v}) { warn "Reading cycle list.\n"; }

my %cycle;
open CYCLES_CFG, "<$opts{cycles}" or die "Can't open $opts{cycles}: $!\n";
my @cycles_cfg = <CYCLES_CFG>;
close CYCLES_CFG;
foreach (@cycles_cfg)
{
    s/\;.*//;
    my ($datasetname, $cycle_id, $wellnumber, $x, $x, @exposures) = split(",");
    $cycle{$cycle_id} = 0;
}

if ($opts{v}) { warn "Checking local files.\n"; }

my @warn;
chdir ($opts{localdir}) or die "$0: Can't chdir to $opts{localdir}: $!\n";
opendir DIR, "."
    or die "$0: Can't read dir $opts{localdir}: $!\n";
foreach my $subdir (sort (readdir DIR))
{
    if (!-d $subdir) { next; }
    if ($subdir eq '.') { next; }
    if ($subdir eq '..') { next; }

    if ($subdir ne '999' && !defined($cycle{$subdir}))
    {
	push (@warn, "Subdir \"$subdir\" is not listed in $opts{cycles}\n");
    }
    $cycle{$subdir} = 1;	# we found it

    opendir SUBDIR, $subdir
	or die "$0: Can't read dir $opts{localdir}/$subdir: $!\n";
    my @raw = grep (/\.raw$/i, readdir SUBDIR);
    if (@raw == ($subdir eq '999' ? $nframes : $nframes * 4))
    {
	unshift (@warn, "Subdir \"$subdir\" is complete.\n");

	foreach my $file (@raw)
	{
	    $file = $subdir . "/" . $file;

	    my $key = $keyprefix . "/" . $file;

	    if ($opts{quick})
	    {
		if (exists($storedmd5{$key}))
		{
		    print STDERR "skip:    ".(" "x32)." $key\n";
		    next;
		}
		if ($opts{n})
		{
		    print STDERR "new:     ".(" "x32)." $key\n";
		    next;
		}
	    }

	    my $data;
	    do {
		local $/ = undef;
		open FILE, "<$file" or die "$0: Can't open $file: $!\n";
		$data = <FILE>;
		close FILE;
	    };

	    my $md5 = " " x 32;

	    if (!$opts{quick})
	    {
		$md5 = md5_hex($data);
		if ($storedmd5{$key} eq $md5)
		{
		    print STDERR "skip:    $md5 $key\n";
		    next;
		}
	    }

	    if (length($storedmd5{$key}) == 32)
	    {
		print STDERR "update:  $md5 $key\n";
	    }
	    else
	    {
		print STDERR "new:     $md5 $key\n";
	    }

	    if (!$opts{n})
	    {
		my $r = $ua->request
		    (POST $remote."/upload.php",
		     "Content-Type" => "form-data",
		     "Content"
		     => [ "key" => $key,
			  "domain" => $opts{domain},
			  "class" => $opts{class},
			  "upload"
			  => [ undef,
			       $file,
			       "Content-Type" => "application/binary",
			       "Content" => $data
			       ]
			  ]
		     );
	    }
	}
    }
    else
    {
	my $nraw = scalar @raw;
	push (@warn, "Subdir \"$subdir\" is incomplete ($nraw images)\n");
    }
}

print STDERR @warn;

# arch-tag: ae2f054a-fd2d-11db-9207-0015f2b17887
